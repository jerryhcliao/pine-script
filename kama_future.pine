// This Pine Script® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © jerryhcliao1123
//@version=6
strategy("KAMA Strategy - Futures", 
     overlay=true, 
     precision=2, 
     initial_capital=500000, 
     pyramiding=0, 
     default_qty_type = strategy.fixed,
     margin_long=100,
     margin_short=100)


// =============== 参数设置 ===============
// KAMA参数
kama_length = input.int(60, title="KAMA Length", minval=1)
kama_fastend = input.float(0.8660, title="KAMA - Fast End", minval=0.01, maxval=1.0, step=0.01)
kama_slowend = input.float(0.0645, title="KAMA - Slow End", minval=0.01, maxval=0.2, step=0.01)

// 线性回归趋势强度参数
trend_period = input.int(12, title="趋势强度检测周期", minval=6, maxval=60, group="趋势强度止盈")
r2_exit_threshold = input.float(0.3, title="R²止盈阈值", minval=0.1, maxval=0.8, step=0.05, group="趋势强度止盈")
slope_decay_period = input.int(6, title="斜率衰减检测周期", minval=3, maxval=60, group="趋势强度止盈")
slope_decay_threshold = input.float(0.5, title="斜率衰减比例", minval=0.01, maxval=0.8, step=0.1, group="趋势强度止盈")

// 仓位和风险参数
risk_percent = input.float(2.0, "风险比例(%)", minval=1, maxval=100, group="风险管理") * 0.01

// ATR参数
atr_length = input.int(60, "ATR周期", minval=1, group="ATR设置")

// ===============  工具函数 ===============
// --- K线时间，方便调试 ---
kTime() =>
    // 获取各个时间组件
    yr = year(time)
    mo = month(time)
    dy = dayofmonth(time)
    hr = hour(time)
    min = minute(time)
    sec = second(time)

    // 转换月、日、时、分、秒为两位数格式
    mo_str = mo < 10 ? "0" + str.tostring(mo) : str.tostring(mo)
    dy_str = dy < 10 ? "0" + str.tostring(dy) : str.tostring(dy)
    hr_str = hr < 10 ? "0" + str.tostring(hr) : str.tostring(hr)
    min_str = min < 10 ? "0" + str.tostring(min) : str.tostring(min)

    // 组合成最终格式
    k_formatted_time = str.tostring(yr) + mo_str + dy_str + hr_str + min_str

    k_formatted_time

isInTradingTimeFrame() => 
    // 计算5年的毫秒数 (5 * 365 * 24 * 60 * 60 * 1000)
    fiveYearsInMs = 5 * 365 * 24 * 60 * 60 * 1000
    // 是否在交易时间范围内
    in_trading_timeframe = time > (timenow - fiveYearsInMs)

    in_trading_timeframe

// 计算KAMA的线性回归斜率和R²
calcTrendStrength(src, length) =>
    // 计算斜率
    lr_current = ta.linreg(src, length, 0)
    lr_previous = ta.linreg(src, length, 1)
    slope = lr_current - lr_previous
    
    // 计算R²
    correlation_coef = ta.correlation(src, bar_index, length)
    r_squared = correlation_coef * correlation_coef
    r_squared := math.max(0, math.min(1, r_squared))
    
    // 转换斜率为百分比
    slope_pct = src > 0 ? (slope / src * 100) : 0
    
    [slope_pct, r_squared]

// =============== 指标计算 ===============
// 基础计算
xPrice = close
xvnoise = math.abs(xPrice - xPrice[1])
nsignal = math.abs(xPrice - xPrice[kama_length])
nnoise = math.sum(xvnoise, kama_length)
nefratio = nnoise != 0 ? nsignal / nnoise : 0

// 计算ATR
atr = ta.atr(atr_length)

// KAMA计算
var float kama = 0.0
kama_smooth = math.pow(nefratio * (kama_fastend - kama_slowend) + kama_slowend, 2)
kama := nz(kama[1]) + kama_smooth * (xPrice - nz(kama[1]))

// 基于斜率判断趋势方向
kama_rising = kama > kama[1]
kama_falling = kama < kama[1]

// 获取当前趋势强度（斜率、R2）
[current_slope, current_r2] = calcTrendStrength(close, trend_period)

// 计算斜率衰减情况
slope_history = ta.sma(math.abs(current_slope), slope_decay_period)
slope_abs = math.abs(current_slope)
slope_is_decaying = slope_abs < (slope_history * slope_decay_threshold)

// =============== 交易信号 ===============
// 交叉信号
bullish_cross = close > kama
bearish_cross = close < kama

// =============== 仓位计算 ==============
// 计算止损点数
stop_points = atr * 2

// 单笔风险
max_loss_amount = strategy.equity * risk_percent

// 计算合适的仓位大小（手数）
position_size_plan = math.floor(max_loss_amount / stop_points / syminfo.pointvalue)

// =============== 止损变量 ===============
// 止损价格
var float stop_price = 0

// =============== 入场条件检查 ===============
// 多头入场：价格上穿KAMA且KAMA向上
long_entry_condition = strategy.position_size == 0 and isInTradingTimeFrame() and
                      bullish_cross and kama_rising and not slope_is_decaying

// 空头入场：价格下穿KAMA且KAMA向下
short_entry_condition = strategy.position_size == 0 and isInTradingTimeFrame() and
                       bearish_cross and kama_falling and not slope_is_decaying

// 多头入场
if long_entry_condition
    stop_price := close - stop_points
    strategy.entry("Long", strategy.long, position_size_plan, comment="多单@" + syminfo.ticker + " 开仓价:" + str.tostring(close, "#.##") + " 止损价:" + str.tostring(stop_price, "#.##"))
    strategy.exit("Long_Stop_Loss", "Long", stop=stop_price, comment=syminfo.ticker + " 多单止损")

// 空头入场
if short_entry_condition
    stop_price := close + stop_points
    strategy.entry("Short", strategy.short, position_size_plan, comment="空单@" + syminfo.ticker + " 开仓价:" + str.tostring(close, "#.##") + " 止损价:" + str.tostring(stop_price, "#.##"))
    strategy.exit("Short_Stop_Loss", "Short", stop=stop_price, comment=syminfo.ticker + " 空单止损")

// =============== 止盈条件检查 ===============
// 不管方向，只要趋势强度衰减就止盈
trend_strength_exit = current_r2 < r2_exit_threshold and slope_is_decaying

long_profit_exit = strategy.position_size > 0 and (trend_strength_exit or bearish_cross)
short_profit_exit = strategy.position_size < 0 and (trend_strength_exit or bullish_cross)

// 执行止盈
if long_profit_exit and strategy.position_size > 0
    strategy.close("Long", comment=syminfo.ticker + " 多单平仓")

if short_profit_exit and strategy.position_size < 0
    strategy.close("Short", comment=syminfo.ticker + " 空单平仓")

// =============== 绘制指标 ===============
// 绘制KAMA均线
plot(kama, color=color.blue, linewidth=2, title="KAMA")

// =============== 信息显示 ===============
// 趋势状态描述
trend_status = current_r2 > r2_exit_threshold ? "强趋势" : "弱趋势"
slope_status = slope_is_decaying ? "衰减" : "稳定"

var label info = label.new(bar_index, na, "", color=color.blue, style=label.style_label_left, textcolor=color.white)
label.set_xy(info, bar_index, high)

label.set_text(info, "KAMA趋势强度策略" + 
     "\n权益：" + str.tostring(math.round(strategy.equity)) + 
     "\n持仓：" + str.tostring(math.round(strategy.position_size)) + 
     "\n浮盈：" + str.tostring(math.round(strategy.openprofit)) +
     "\n斜率：" + str.tostring(slope_abs, "#.##") + "%" +
     "\n斜率-SMA：" + str.tostring(slope_history, "#.##") + "%" +
     "\nR²：" + str.tostring(current_r2, "#.##") +
     "\n趋势：" + trend_status + " " + slope_status)